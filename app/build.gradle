plugins {
    id "com.safetorun.builder"
    id 'org.jetbrains.kotlinx.kover'
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
def directory = "$rootProject.projectDir/scripts/dependencies/"
apply from: "$directory/android_test_dependencies.gradle"


android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.andro.secure"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    buildTypes {
        def serverUrl = System.getenv("SERVER_URL") ?: SERVER_URL
        def apiKey = System.getenv("API_KEY") ?: API_KEY

        debug {
            buildConfigField "String", "SERVER_URL", "\"$serverUrl\""
            buildConfigField "String", "API_KEY", "\"$apiKey\""
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "SERVER_URL", "\"$serverUrl\""
            buildConfigField "String", "API_KEY", "\"$apiKey\""
        }
    }
    namespace 'com.andro.secure'
}

dependencies {

    def nav_version = "2.6.0"

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation project(":safeToRun")
    implementation project(":safeToRunInputValidation")
    implementation project(":safeToRunLogger")
    implementation project(":safeToRunPlus")
    implementation("com.auth0:java-jwt:$jwtVersion")
    implementation "com.airbnb.android:epoxy:$epoxyVersion"
    implementation "com.airbnb.android:epoxy-databinding:$epoxyVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerView"
    kapt "com.airbnb.android:epoxy-processor:$epoxyVersion"
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
}

apply plugin: 'firebase.test.lab'

firebaseTestLab {
    keyFile = file("../google-service-key.json")
    googleProjectId = "andro-3982e"
    devices {
        nexusEmulator {
            deviceIds = ["redfin", "star2lte", "judypn"]
            androidApiLevels = [28, 29, 30]
        }
    }
}

safeToRun {
    configurationPath = "buildSrc/configuration.json"
    generatedCodePath = "src/main/java"
}
